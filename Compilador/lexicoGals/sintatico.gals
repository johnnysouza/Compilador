#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
min: [a-z]
mai: [A-Z]
letra: {mai} | {min}
digito: [0-9]
caracter: [^ \n]
barra: "/"
comment_Linha: {barra}{barra}{caracter}*
comment_Bloco: \{ [^}]* \}
#Tokens
// identificador
identificador: ({letra} | _{letra}) ( _{letra} | {letra} | {digito})*

// cte integer
constanteInteira: 0 | [1-9] {digito}? {digito}? {digito}? {digito}?
// cte float
constanteFloat: (0 | [1-9] {digito}? {digito}? {digito}? {digito}?) , ({digito}? {digito}? {digito}? {digito}? [1-9] | 0)
// cte string
constanteString: \" ( [^ \" \n \\ ] | \\\\ | \\\" )* \"

// comentarios: o linha e de bloco - em como ignorar
:! {comment_Linha}
:! {comment_Bloco}

// simbolos especiais
"+"
"-"
"*"
"/"
"="
"&&"
"||"
"!"
"=="
"!="
"<"
"<="
">"
">="
","
";"
"("
")"

// palavras reservadas
pr_boolean = identificador : "boolean"
pr_do = identificador : "do"
pr_else = identificador : "else"
pr_end = identificador : "end"
pr_false = identificador : "false"
pr_float = identificador : "float"
pr_if = identificador : "if"
pr_integer = identificador : "integer"
pr_main = identificador : "main"
pr_print = identificador : "print"
pr_println = identificador : "println"
pr_scan = identificador : "scan"
pr_string = identificador : "string"
pr_true = identificador : "true"
pr_void = identificador : "void"
pr_while = identificador : "while"
pr_global = identificador : "global"
pr_local = identificador : "local"
pr_return = identificador : "return"

// caracteres de formatação - ignorados
:[\t\s\n]
#NonTerminals
<main>

<variaveis_globais>
<lista_modulos>
<lista_comandos>
<modulo>
<tipo_modulo>
<parametros>
<lista_parametros>
<parametro>
<tipo>
<variaveis_locais>
<retorno>
<variaveis>
<lista_identificadores>
<valor>
<constante>
<comando>
<cmd_atribuicao>
<cmd_entrada>
<cmd_saida>
<lista_expressoes>
<cmd_selecao>
<else>
<cmd_repeticao>
<cmd_chamada_modulo>
<parametros_reais>
<expressao>
<lista_parametros_1>
<lista_identificadores_1>
<lista_expressoes_1>
<expressao_1>
<relacional_1>
<aritmetica_1>
<termo_1>
<identificador_1>
<elemento>
<relacional>
<operador_relacional>
<aritmetica>
<termo>
<fator>
<comando_1>
#Grammar
<main> ::= #15 pr_main <variaveis_globais> <lista_modulos> <lista_comandos> pr_end #16 ;

<variaveis_globais> ::= pr_global <variaveis> <variaveis_globais> | î ;
<lista_modulos> ::= î | <modulo> <lista_modulos> ;
<lista_comandos> ::= î | <comando> <lista_comandos> ;
<modulo> ::= <tipo_modulo> identificador "(" <parametros> ")" <variaveis_locais> <lista_comandos> <retorno> pr_end ;
<tipo_modulo> ::= pr_boolean | pr_float | pr_integer | pr_string | pr_void ;
<parametros> ::= î | <lista_parametros> ;
<lista_parametros> ::= <parametro> <lista_parametros_1> ;
<lista_parametros_1> ::= î | "," <lista_parametros> ;
<parametro> ::= <tipo> identificador ;
<tipo> ::= pr_boolean | pr_float | pr_integer | pr_string ;
<variaveis_locais> ::= î | pr_local <variaveis> <variaveis_locais> ;
<retorno> ::= î | pr_return <expressao> ";" ;
<variaveis> ::= <tipo> #24 <lista_identificadores> #26 <valor> ";" ;
<lista_identificadores> ::= identificador #25 <lista_identificadores_1> ;
<lista_identificadores_1> ::= î | "," <lista_identificadores> ;
<valor> ::= î | "=" <constante> #30 ;
<constante> ::= constanteInteira | constanteFloat | constanteString | pr_true | pr_false ;
<comando> ::= identificador #25 <comando_1> | <cmd_entrada> | <cmd_saida> | <cmd_selecao> | <cmd_repeticao> ;
<comando_1> ::= <cmd_atribuicao> | <cmd_chamada_modulo> ;
<cmd_atribuicao> ::= "=" <expressao> #29 ";" ;
<cmd_entrada> ::= pr_scan "(" <lista_identificadores> #27 ")" ";" ;
<cmd_saida> ::= pr_print "(" <lista_expressoes> ")" ";" | pr_println "(" <lista_expressoes> #17 ")" ";" ;
<lista_expressoes> ::= <expressao> #14 <lista_expressoes_1> ;
<lista_expressoes_1> ::= î | "," <lista_expressoes> ;
<cmd_selecao> ::= pr_if "(" <expressao> ")" #31 <lista_comandos> <else> pr_end #32 ";" ;
<else> ::= pr_else #33 <lista_comandos> | î ;
<cmd_repeticao> ::= pr_do #34 <lista_comandos> pr_while "(" <expressao> ")" #35 ";" ;
<cmd_chamada_modulo> ::= "(" <parametros_reais> ")" ";" ;
<parametros_reais> ::= î | <lista_expressoes> ;

<expressao> ::= <elemento> <expressao_1> ;
<expressao_1> ::= î | "||" <elemento> #18 <expressao_1> | "&&" <elemento> #19 <expressao_1> ;
<elemento> ::= <relacional> | pr_true #11 | pr_false #12 | "!" <elemento> #13 ;
<relacional> ::= <aritmetica> <relacional_1> ;
<relacional_1> ::= î | <operador_relacional> #20 <aritmetica> #21 ;
<operador_relacional> ::= "==" | "!=" | "<" | "<=" | ">" | ">=" ;
<aritmetica> ::= <termo> <aritmetica_1> ;
<aritmetica_1> ::= î | "+" <termo> #1 <aritmetica_1> | "-" <termo> #2 <aritmetica_1> ;
<termo> ::= <fator> <termo_1> ;
<termo_1> ::= î | "*" <fator> #3 <termo_1> | "/" <fator> #4 <termo_1> ;
<fator> ::= identificador #28 <identificador_1> 
			| constanteInteira #5
			| constanteFloat #6
			| constanteString #22
			| "(" <expressao> ")" 
			| "+" <fator> #23
			| "-" <fator> #7 ;
<identificador_1> ::= î | "(" <lista_expressoes> ")" ;
